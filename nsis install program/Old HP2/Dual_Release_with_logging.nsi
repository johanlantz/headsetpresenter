; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "HeadsetPresenter"
!define PRODUCT_VERSION "2.0"
!define PRODUCT_PUBLISHER "HeadsetPresenter"
!define PRODUCT_WEB_SITE "http://www.headsetpresenter.com"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!define MUI_LICENSEPAGE_RADIOBUTTONS
!insertmacro MUI_PAGE_LICENSE "license.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

!addplugindir ".\plugins"   ;NSIS searches this directory for plugins. Here the sysinfo and sysinfoBT dll's are located
; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "SetupWithLogging.exe"
InstallDir "$PROGRAMFILES\HeadsetPresenter"
ShowInstDetails nevershow
ShowUnInstDetails nevershow

Var VersionToInstall  ;Can be MSVersion or GenericVersion

Section -CheckWhichVersionToInstall
  ;Step 1 Check Major version 5 is 2000 or XP. 4 is all the old ones
  sysinfo::GetSystemVersionValue 'MajorVersion'
  Pop $0
  StrCmp $0 "4" lbl_NonSupportedWindowsRelease

  ;Step 2 Check minor version, 0 is 2000, 1 is XP
  sysinfo::GetSystemVersionValue 'MinorVersion'
  Pop $0
  StrCmp $0 "0" lbl_InstallGenericVersion
    goto lbl_UserHasXPCheckServicePackVersion

  ;Step 3 Check if XP SP1 or SP2 is installed. Bluetooth functions in sysinfo might not work on SP1 unless special drivers are installed
  lbl_UserHasXPCheckServicePackVersion:
  sysinfo::GetSystemVersionValue 'ServicePackMajorVersion'
  Pop $0
  StrCmp $0 "1" lbl_XPServicePack1Detected
    goto lbl_UserHasXPCheckIfMSStack

  ;The user has Windows XP but SP1 installed. Query how we should proceed
  lbl_XPServicePack1Detected:
    MessageBox MB_YESNO|MB_ICONINFORMATION "The HeadsetPresenter comes in two versions.$\n$\nOne for Microsoft Bluetooth support in Windows XP$\nand one for drivers supplied with your Bluetooth adaptor.$\n$\nSince you run Windows XP SP1 the installer can not determine which version to install.$\n$\nThe installer will default to the non-Microsoft version.$\n$\nIf you are certain that you use the Microsoft Bluetooth stack$\nclick NO below and the Microsoft version will be installed instead.$\n$\nDo you want to install the HeadsetPresenter for non-Microsoft Bluetooth drivers?" IDYES lbl_InstallGenericVersion IDNO lbl_InstallMSVersion
  goto lbl_done

  ;From the MinorVersion we know that the user has XP. Now we must check which stack he uses
  lbl_UserHasXPCheckIfMSStack:
    sysinfoBT::GetUsedStack
    Pop $0
    StrCmp $0 "MSStack" lbl_InstallMSVersion
      goto lbl_InstallGenericVersionOnXP
    goto lbl_InstallMSVersion

  ;A generic stack on XP is detected, tell the user before installing
  lbl_InstallGenericVersionOnXP:
    ;MessageBox MB_YESNO|MB_ICONINFORMATION "This MessageBox will be removed later.$\n$\nYou are NOT using the Microsoft Bluetooth support in Windows XP.$\nThe Generic version of the HP will be installed.$\nIf this is not correct please let me know asap.$\n$\nDo you want to install the generic version?" IDYES lbl_InstallGenericVersion IDNO lbl_InstallMSVersion
    goto lbl_InstallGenericVersion
  goto lbl_done

  ;Installing the Generic version
  lbl_InstallGenericVersion:
    ;MessageBox MB_OK|MB_ICONINFORMATION "This MessageBox will be removed later. $\n$\nWindows 2000 is used and the generic version will be installed."
    StrCpy $VersionToInstall "GenericVersion"
  goto lbl_done

  lbl_InstallMSVersion:
    ;MessageBox MB_YESNO|MB_ICONINFORMATION "This MessageBox will be removed later.$\n$\nYou are using the Microsoft Bluetooth support in Windows XP.$\nThe Microsoft version of HP will be installed.$\n$\nIf this is not correct please let me know asap.$\n$\nDo you want to install the MS version?" IDNO lbl_InstallGenericVersion
    StrCpy $VersionToInstall "MSVersion"
  goto lbl_done

  lbl_NonSupportedWindowsRelease:
    MessageBox MB_OK|MB_ICONINFORMATION "Your operating system is not supported by the HeadsetPresenter.$\nPlease use Windows 2000 or Windows XP."
  goto lbl_done

  lbl_done:

SectionEnd

Section "MainSection" SEC01
  SetOverwrite on

  SetOutPath "$WINDIR"
  ;Always include the SS_Log_Server.exe, needs to be in the Windows directory
  File "D:\other projects\SS_Log_Src\SS_Log\SS_Log_Server\Release\SS_Log_Server.exe"

  SetOutPath "$INSTDIR"
  ;Always include the LicenceManager
  File "..\LicenseManager\Release with Log\LicenseManager.dll"
  File "..\LicenseManager\CRC\lmlogg.blk"
  ;Include HPSpeech even if it cannot be used for MS, makes the installer easier to read
  File "..\HPSpeech\Release with Log\HPSpeech.dll"
  
  ;Check to see which files to install
  StrCmp $VersionToInstall "MSVersion" lbl_MSVersion
    goto lbl_GenericVersion
    
  lbl_MSVersion:
    File "..\HS_gen\Release_with_Log\HeadsetPresenter.dll"
    File "..\HS_gen\Release_Stand_Alone_With_Log\HeadsetPresenter.exe"
  goto lbl_doneVersions
  
  lbl_GenericVersion:
    File "..\HS_gen\Generic_Release_with_Log\HeadsetPresenter.dll"
    File "..\HS_gen\Generic_Release_Stand_Alone_With_Log\HeadsetPresenter.exe"
    File "..\HPSpeech\HPGrammar.xml"
    File "..\Configurator\Release with Log\Configurator.exe"
    File "..\HS_gen\SettingsFile\HPConfig.txt" ;For Jabra this would be with forward only preset
    SetOutPath "$SYSDIR\drivers"
    File "..\HS_gen\filter\release2k\hspf.sys"
  goto lbl_doneVersions
  
  lbl_doneVersions:
  SetOutPath "$INSTDIR"
     File "..\Documentation\HeadsetPresenter_For_Beginners.pdf"
  SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateDirectory "$SMPROGRAMS\HeadsetPresenter"
  CreateShortCut "$SMPROGRAMS\HeadsetPresenter\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\HeadsetPresenter\Uninstall.lnk" "$INSTDIR\uninst.exe"
  CreateShortCut "$SMPROGRAMS\HeadsetPresenter\HeadsetPresenter For Beginners.lnk" "$INSTDIR\HeadsetPresenter_For_Beginners.pdf"
  CreateShortCut "$SMPROGRAMS\HeadsetPresenter\HeadsetPresenter Stand Alone.lnk" "$INSTDIR\HeadsetPresenter.exe"

  StrCmp $VersionToInstall "GenericVersion" lbl_installGenericApps
    goto lbl_installMSapps

  lbl_installGenericApps:
      CreateShortCut "$SMPROGRAMS\HeadsetPresenter\Configurator.lnk" "$INSTDIR\Configurator.exe"
  goto lbl_doneInstallApps
  
  lbl_installMSApps:
  goto lbl_doneInstallApps
  
  lbl_doneInstallApps:
SectionEnd

Section -Post
  RegDLL "$INSTDIR\LicenseManager.dll"
  RegDLL "$INSTDIR\HeadsetPresenter.dll"
  RegDLL "$INSTDIR\HPSpeech.dll"

  IfErrors 0 lbl_NoError
    MessageBox MB_ICONINFORMATION|MB_YESNO "Installation if the HeadsetPresenter failed.$\n$\nMost likely the problem is that you do not have Admin rights for this computer and are not allowed to install new applications.$\n$\nTalk to the IT support responsible at your company or institution to correct this.$\n$\nIf you are certain that you have the necessary priviliges contact support to see if the problem can be resolved in another way.$\n$\nMore information might be available on the HeadsetPresenter website. Do you want to be directed there?" IDYES lbl_ExecuteHelp IDNO lbl_NoError
  lbl_ExecuteHelp:
    ;ExecShell open '"%comspec%" start http://www.headsetpresenter.com/content/help.asp?codeID=150'
    ExecShell open "http://www.headsetpresenter.com/content/help.asp?codeID=150"
  ;Here we proceed to lbl_NoError even if we failed, otherwise we will not get the uninstaller
  lbl_NoError:
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$WINDIR\SS_Log_Server.exe"

  UnRegDLL "$INSTDIR\HeadsetPresenter.dll"
  UnRegDLL "$INSTDIR\LicenseManager.dll"
  UnRegDLL "$INSTDIR\HPSpeech.dll"
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\HeadsetPresenter.dll"
  Delete "$INSTDIR\HeadsetPresenter.exe"
  Delete "$INSTDIR\..\HPSpeech\Release with Log\HPSpeech.dll"
  Delete "$INSTDIR\LicenseManager.dll"
  Delete "$INSTDIR\*.lic"
  Delete "$INSTDIR\HeadsetPresenter_For_Beginners.pdf"
  Delete "$SMPROGRAMS\HeadsetPresenter\Uninstall.lnk"
  Delete "$SMPROGRAMS\HeadsetPresenter\Website.lnk"
  Delete "$SMPROGRAMS\HeadsetPresenter\HeadsetPresenter For Beginners.lnk"
  Delete "$SMPROGRAMS\HeadsetPresenter\HeadsetPresenter Stand Alone.lnk"

  ;XP file to delete, always try to delete them since VersionToInstall does not work in the Uninstall section for some reason
  Delete "$INSTDIR\LastUsedHeadset.txt"
  
  ;Generic files, always try to delete them since VersionToInstall does not work in the Uninstall section for some reason
  Delete "$SYSDIR\drivers\hspf.sys"
  Delete "$INSTDIR\Configurator.exe"
  Delete "$INSTDIR\HPConfig.txt"
  Delete "$INSTDIR\DeviceInfo.txt"
  Delete "$SMPROGRAMS\HeadsetPresenter\Configurator.lnk"
  RMDir "$SMPROGRAMS\HeadsetPresenter"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd
