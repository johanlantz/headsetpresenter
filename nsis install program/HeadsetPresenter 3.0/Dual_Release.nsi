; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "HeadsetPresenter"
!define PRODUCT_VERSION "3.5"
!define PRODUCT_PUBLISHER "HeadsetPresenter"
!define PRODUCT_WEB_SITE "http://www.headsetpresenter.com"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!define MUI_LICENSEPAGE_RADIOBUTTONS
!insertmacro MUI_PAGE_LICENSE "license.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

!addplugindir "..\plugins"   ;NSIS searches this directory for plugins. Here the sysinfo and sysinfoBT dll's are located
; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Setup.exe"
InstallDir "$PROGRAMFILES\HeadsetPresenter 3.5"
ShowInstDetails nevershow
ShowUnInstDetails nevershow

Var VersionToInstall  ;Can be
Var OSMajorVersion
Var OSMinorVersion
Section -CheckWhichVersionToInstall

  ;Step 0 Check if a valid Widcomm stack is installed, if so OS should not matter just install Widcomm version
  
  ;goto lbl_InstallWidcommVersion  ;FOR NOW ALWAYS INSTALL WIDCOMM
  ;sysinfoBTWidcomm::CheckWidcomm
  ;Pop $0
  ;StrCmp $0 "WidcommStack_OK" lbl_InstallWidcommVersion

  ;Step 1 Check Major version 6 is Vista, 5 is 2000 or XP. 4 is all the old ones
  sysinfo::GetSystemVersionValue 'MajorVersion'
  Pop $0

  StrCpy $OSMajorVersion $0    ;Store OS version for OS specific settings
  
  ;MessageBox MB_OK $0
  StrCmp $0 "6" lbl_MS_Support  ;is vista
  StrCmp $0 "7" lbl_MS_Support ;For future not used here

 ;Step 2 Check minor version, 0 is 2000, 1 is XP
  sysinfo::GetSystemVersionValue 'MinorVersion'
  Pop $0
  StrCpy $OSMinorVersion $0    ;Store OS version for OS specific settings, which driver to use
  StrCmp $0 "0" lbl_noMS_Support  ;is 2000 no Microsoft support
    goto lbl_UserHasXPCheckServicePackVersion  ;use has XP check servicepack

;Step 3 Check if XP SP1 or SP2 is installed. Bluetooth functions in sysinfo might not work on SP1 unless special drivers are installed
  lbl_UserHasXPCheckServicePackVersion:
  sysinfo::GetSystemVersionValue 'ServicePackMajorVersion'
  Pop $0
  StrCmp $0 "1" lbl_XPServicePack1Detected
  StrCmp $0 "2" lbl_MS_Support   ;SP2 installed has Microsoft drivers
    goto lbl_noMS_Support   ;No SP installed no Microsoft drivers

;The user has Windows XP but SP1 installed. Query how we should proceed
  lbl_XPServicePack1Detected:
    MessageBox MB_YESNO|MB_ICONINFORMATION "Since you run Windows XP SP1 the installer can not determine if you use Microsoft Bluetooth drivers.$\n$\nThe installer will select the non-Microsoft option by default.$\n$\nIf you are certain that you use the Microsoft Bluetooth stack$\nclick NO below and the Microsoft version will be installed. If the Microsoft drivers are not present, HeadsetPresenter will crash on startup.$\n$\nDo you want to install the HeadsetPresenter for non-Microsoft Bluetooth drivers (recommended if unsure)?" IDYES lbl_noMS_Support IDNO lbl_MS_Support
  goto lbl_done

lbl_MS_Support:
  StrCpy $VersionToInstall "MSVersion"
  ;MessageBox MB_OK "installing with ms support"
goto lbl_Done

lbl_noMS_Support:
  ;MessageBox MB_OK "installing without ms support"
  StrCpy $VersionToInstall "nonMSVersion"
goto lbl_Done

 

  lbl_done:

SectionEnd

Section "MainSection" SEC01
  SetOverwrite on
  
  SetOutPath "$WINDIR"
  ;Always include the SS_Log_Server.exe, needs to be in the Windows directory
  File "C:\Projects\SS_Log_Src\SS_Log\SS_Log_Server\Release\SS_Log_Server.exe"

  SetOutPath "$INSTDIR"
  ;Always include the LicenceManager
  File "..\..\LicenseManager\Release\LicenseManager.dll"
  File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\pushkeys\Dll\PushDLL.dll"
  File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\Speech\Grammar\HPGrammar.xml"
  File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\HeadsetPresenter.exe.manifest"

  ;Include Bluetols always for regdll simplicity, if MS stack works with Bluetools it is needed anyway
  File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\Bluetools\bin\BlueTools.dll"
  File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\Bluetools\bin\BlueToolsMS.dll"
  File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\Bluetools\bin\BlueToolsWC.dll"
  File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\Bluetools\bin\BlueToolsWC150.dll"
  
  ;Include BlueSoleil BSProxy.dll
  File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\BlueSoleil\BSProxy\Release\BSProxy.dll"
  
  ;Including ButtonMappings for all releases
  File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\SettingsFiles\ButtonMappings.txt"


  ;Include HPSpeech even if it cannot be used for MS, makes the installer easier to read
 # File "..\..\HPSpeech\Release\HPSpeech.dll"

  ;Include advance mode option, there are 3 drivers,Vista, XP, 2K ,,, Check server2003? using old one now
  SetOutPath "$SYSDIR\drivers"
  strcmp $OSMajorVersion "7" lbl_VistaDriver   ;6 = Future windows
  strcmp $OSMajorVersion "6" lbl_VistaDriver   ;6 = Vista
  strcmp $OSMajorVersion "5" lbl_CheckMinorVersion lbl_DoneDriver   ;advanced mode not supported on older then MajorVersion = 5

  lbl_CheckMinorVersion:
  strcmp $OSMinorVersion "2" lbl_2003ServerDriver
  strcmp $OSMinorVersion "1" lbl_XPDriver
  strcmp $OSMinorVersion "0" lbl_OldDriver  lbl_XPDriver  ;go for the XP driver if no match? should never happen
  
  
  
  lbl_VistaDriver:
    File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\USBSnoop\2.0\objfre_wlh_x86\i386\hspf.sys"  ;Vista specific USBSnoop 2.0
  goto lbl_DoneDriver

  lbl_2003ServerDriver:
    File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\USBSnoop\2.0\objfre_wnet_x86\i386\hspf.sys"  ;Server2003 specific USBSnoop 2.0
  goto lbl_DoneDriver

  lbl_XPDriver:
    File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\USBSnoop\2.0\objfre_wxp_x86\i386\hspf.sys"  ;XP specific USBSnoop 2.0
  goto lbl_DoneDriver

  lbl_OldDriver:
    ;File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\USBSnoop\2.0\objfre_w2k_x86\i386\hspf.sys"  ;XP specific USBSnoop 2.0
    File "..\..\HeadsetPresenter_Bluetools\Win2kSnoopDriver\filter\release2k\hspf.sys" ;2k driver , no IOCTL support
  goto lbl_DoneDriver
  
  lbl_DoneDriver:  ;Driver selection done, continue
  
  
  ;Reset to install dir so we do not copy all files to drivers dir
  SetOutPath "$INSTDIR"
  
  ;Check to see which files to install
  StrCmp $VersionToInstall "MSVersion" lbl_MSVersion
  StrCmp $VersionToInstall "nonMSVersion" lbl_nonMSVersion

  lbl_MSVersion:   ;this actually means XP SP2 or higher
    File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\MS_Release\HeadsetPresenter.exe"
  goto lbl_doneVersions
  
  lbl_nonMSVersion:  ;XP SP1 or lower
    File "..\..\HeadsetPresenter_Bluetools\HeadsetPresenter\Release\HeadsetPresenter.exe"
  goto lbl_doneVersions
  
  goto lbl_doneVersions
  
  lbl_doneVersions:
  SetOutPath "$INSTDIR"
     File "..\..\Documentation\HeadsetPresenter_For_Beginners.pdf"
  SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateDirectory "$SMPROGRAMS\HeadsetPresenter 3.5"
  CreateShortCut "$SMPROGRAMS\HeadsetPresenter 3.5\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\HeadsetPresenter 3.5\Uninstall.lnk" "$INSTDIR\uninst.exe"
  CreateShortCut "$SMPROGRAMS\HeadsetPresenter 3.5\HeadsetPresenter For Beginners.lnk" "$INSTDIR\HeadsetPresenter_For_Beginners.pdf"
  CreateShortCut "$SMPROGRAMS\HeadsetPresenter 3.5\HeadsetPresenter 3.5.lnk" "$INSTDIR\HeadsetPresenter.exe"
SectionEnd

Section -Post  ;must register Bluetools
  RegDLL "$INSTDIR\LicenseManager.dll"
  
  IfErrors 0 lbl_NoError
    MessageBox MB_ICONINFORMATION|MB_YESNO "Installation if the HeadsetPresenter failed.$\n$\nMost likely the problem is that you do not have Admin rights for this computer and are not allowed to install new applications.$\n$\nTalk to the IT support responsible at your company or institution to correct this.$\n$\nIf you are certain that you have the necessary priviliges contact support to see if the problem can be resolved in another way.$\n$\nMore information might be available on the HeadsetPresenter website. Do you want to be directed there?" IDYES lbl_ExecuteHelp IDNO lbl_NoError
  lbl_ExecuteHelp:
    ;ExecShell open '"%comspec%" start http://www.headsetpresenter.com/content/help.asp?codeID=150'
    ExecShell open "http://www.headsetpresenter.com/content/help.asp?codeID=150"
  ;Here we proceed to lbl_NoError even if we failed, otherwise we will not get the uninstaller
  lbl_NoError:
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  ;UnRegDLL "$INSTDIR\LicenseManager.dll"
  ;UnRegDLL "$INSTDIR\HPSpeech.dll"
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\*.*"
  Delete "$SMPROGRAMS\HeadsetPresenter 3.5\*.*"

  ;Generic files, always try to delete them since VersionToInstall does not work in the Uninstall section for some reason
  ;Delete "$SYSDIR\drivers\hspf.sys"
  RMDir "$SMPROGRAMS\HeadsetPresenter 3.5"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd
